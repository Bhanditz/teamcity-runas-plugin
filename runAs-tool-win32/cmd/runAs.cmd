@ECHO OFF
SETLOCAL
SET /A ARGS_COUNT=0    
FOR %%A in (%*) DO SET /A "ARGS_COUNT+=1"
IF %ARGS_COUNT% NEQ 2 (
	@ECHO Invalid arguments.
	@ECHO Usage: runAs.cmd credentials_file command
	@EXIT -1
)
ENDLOCAL

REM Create user for tests
net user RunAsTestUser /delete>NUL
net user /add /active:yes RunAsTestUser aaa>NUL
net localgroup Administrators RunAsTestUser /ADD>NUL

REM Define OS bitness
SET "RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_BIN=%~dp0"
SET "RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_TOOL=JetBrains.runAs.exe"
%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_BIN%x86\%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_TOOL% -t
SET "RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_OS_BITNESS=%errorlevel%"

IF %RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_OS_BITNESS% EQU 64 (
	SET "RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_TOOL=%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_BIN%x64\%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_TOOL%"
	GOTO RUN_AS
)

IF %RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_OS_BITNESS% EQU 32 (
	SET "RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_TOOL=%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_BIN%x86\%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_TOOL%"
	GOTO RUN_AS
)

SET "EXIT_CODE=%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_OS_BITNESS%"
ECHO.
IF %EXIT_CODE% EQU 1 ECHO ##teamcity[message text='Invoker has no administrative privileges, when running under the Windows service as LocalSystem.' status='ERROR']
IF %EXIT_CODE% EQU 2 ECHO ##teamcity[message text='Invoker has no SeAssignPrimaryTokenPrivilege privilege, when running under the Windows service as LocalSystem.' status='ERROR']
EXIT /B %EXIT_CODE%

:RUN_AS
SET "RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_BIN="
SET "RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_="

REM Run command line
%RUNAS_76200936_0AA1_4855_A204_05C3F3C54476_PATH_TO_TOOL% -i:auto -l:normal -c:%1 -b:-10000 cmd.exe /C %2
SET "EXIT_CODE=%ERRORLEVEL%"

ECHO.
IF %EXIT_CODE% EQU -10000 ECHO ##teamcity[message text='Unknown error occurred.' status='ERROR']
IF %EXIT_CODE% EQU -10001 ECHO ##teamcity[message text='Invalid usage of the tool.' status='ERROR']
IF %EXIT_CODE% EQU -10002 ECHO ##teamcity[message text='Security error occurred.' status='ERROR']
IF %EXIT_CODE% EQU -10003 ECHO ##teamcity[message text='WIN32 API error occurred.' status='ERROR']

EXIT /B %EXIT_CODE%